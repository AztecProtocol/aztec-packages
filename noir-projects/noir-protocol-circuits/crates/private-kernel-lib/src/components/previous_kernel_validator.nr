use dep::types::{
    abis::kernel_circuit_public_inputs::PrivateKernelCircuitPublicInputs, utils::arrays::array_length,
    traits::is_empty
};

struct PreviousKernelValidator {
    previous_kernel: PrivateKernelCircuitPublicInputs
}

impl PreviousKernelValidator {
    pub fn new(previous_kernel: PrivateKernelCircuitPublicInputs) -> Self {
        PreviousKernelValidator { previous_kernel }
    }

    pub fn validate_for_private_tail(self) {
        self.validate_empty_private_call_stack();
        self.validate_empty_public_call_stack();
        self.verify_empty_validation_requests();
        self.verify_no_transient_data();
    }

    pub fn validate_for_private_tail_to_public(self) {
        self.validate_empty_private_call_stack();
        self.validate_non_empty_public_call_stack();
        self.verify_empty_validation_requests();
        self.verify_no_transient_data();
    }

    fn validate_empty_private_call_stack(self) {
        assert_eq(
            array_length(self.previous_kernel.end.private_call_stack), 0, "Private call stack must be empty when executing the tail circuit"
        );
    }

    fn validate_empty_public_call_stack(self) {
        assert_eq(
            array_length(self.previous_kernel.end.public_call_stack), 0, "Public call stack must be empty when executing the tail circuit"
        );
        assert(
            is_empty(self.previous_kernel.public_teardown_call_request), "Public teardown call request must be empty when executing the tail circuit"
        );
    }

    fn validate_non_empty_public_call_stack(self) {
        let call_stack_len = array_length(self.previous_kernel.end.public_call_stack);
        assert(
            (call_stack_len != 0) | !is_empty(self.previous_kernel.public_teardown_call_request), "Must have public calls when exporting public kernel data from the tail circuit"
        );
    }

    fn verify_empty_validation_requests(self) {
        assert_eq(
            array_length(self.previous_kernel.validation_requests.note_hash_read_requests), 0, "Non empty note hash read requests"
        );
        assert_eq(
            array_length(self.previous_kernel.validation_requests.nullifier_read_requests), 0, "Non empty nullifier read requests"
        );
        assert_eq(
            array_length(self.previous_kernel.validation_requests.scoped_key_validation_requests_and_generators), 0, "Non empty key validation requests"
        );
    }

    fn verify_no_transient_data(self) {
        // Currently all the transient note hashes and nullifiers must be cleared in the reset circuits.
        // Check that the propagated note hashes don't link to a nullifier, and vice versa.
        for note_hash in self.previous_kernel.end.new_note_hashes {
            assert_eq(note_hash.nullifier_counter, 0, "Unresolved transient note hash");
        }
        for new_nullifier in self.previous_kernel.end.new_nullifiers {
            assert_eq(new_nullifier.nullified_note_hash(), 0, "Unresolved transient nullifier");
        }
    }
}
