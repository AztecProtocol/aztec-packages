use crate::{
    abis::side_effect::Ordered, traits::{Empty, is_empty},
    utils::arrays::{
    sort_by_counter::{compare_by_counter_empty_padded_asc, compare_by_counter_empty_padded_desc},
    get_sorted_tuple::get_sorted_tuple
}
};

struct SplitOrderHints<let N: u32> {
    sorted_counters_lt: [u32; N],
    sorted_counters_gte: [u32; N],
    sorted_indexes: [u32; N],
}

impl<let N: u32> SplitOrderHints<N> {
    pub fn empty() -> SplitOrderHints<N> {
        SplitOrderHints { sorted_counters_lt: [0; N], sorted_counters_gte: [0; N], sorted_indexes: [0; N] }
    }
}

impl<let N: u32> Eq for SplitOrderHints<N> {
    fn eq(self, other: Self) -> bool {
        (self.sorted_counters_lt == other.sorted_counters_lt)
            & (self.sorted_counters_gte == other.sorted_counters_gte)
            & (self.sorted_indexes == other.sorted_indexes)
    }
}

fn get_split_order_hints<T, let N: u32>(
    array: [T; N],
    split_counter: u32,
    ascending: bool
) -> SplitOrderHints<N> where T: Ordered + Eq + Empty {
    let ordering = if ascending {
        compare_by_counter_empty_padded_asc
    } else {
        compare_by_counter_empty_padded_desc
    };
    let sorted_tuples = get_sorted_tuple(array, ordering);

    let mut sorted_counters_lt = [0; N];
    let mut sorted_counters_gte = [0; N];
    let mut sorted_indexes = [0; N];
    let mut num_lt = 0;
    let mut num_gte = 0;
    let mut found_split = false;
    for i in 0..N {
        let elem = sorted_tuples[i].elem;
        if !is_empty(elem) {
            let is_gte = (elem.counter() >= split_counter) & (elem.counter() != 0);
            found_split |= ascending == is_gte;
            let mut index_offet = 0;
            if found_split != ascending {
                sorted_counters_lt[num_lt] = elem.counter();
                num_lt += 1;
                index_offet = num_gte;
            } else {
                sorted_counters_gte[num_gte] = elem.counter();
                num_gte += 1;
                index_offet = num_lt;
            }
            let original_index = sorted_tuples[i].original_index;
            sorted_indexes[original_index] = if !found_split { i } else { i - index_offet };
        }
    }

    SplitOrderHints { sorted_counters_lt, sorted_counters_gte, sorted_indexes }
}

pub fn get_split_order_hints_asc<T, let N: u32>(
    array: [T; N],
    split_counter: u32
) -> SplitOrderHints<N> where T: Ordered + Eq + Empty {
    get_split_order_hints(array, split_counter, true)
}

pub fn get_split_order_hints_desc<T, let N: u32>(
    array: [T; N],
    split_counter: u32
) -> SplitOrderHints<N> where T: Ordered + Eq + Empty {
    get_split_order_hints(array, split_counter, false)
}

mod tests {
    use crate::{
        tests::types::TestValue,
        utils::arrays::assert_split_sorted_transformed_value_arrays::get_split_order_hints::{get_split_order_hints_asc, get_split_order_hints_desc, SplitOrderHints}
    };

    global full_array = [
        TestValue { value: 100, counter: 11 },
        TestValue { value: 200, counter: 17 },
        TestValue { value: 300, counter: 7 },
        TestValue { value: 400, counter: 5 },
        TestValue { value: 500, counter: 13 }
    ];

    global padded_array = [
        TestValue { value: 100, counter: 11 },
        TestValue { value: 200, counter: 17 },
        TestValue { value: 300, counter: 7 },
        TestValue { value: 400, counter: 5 },
        TestValue { value: 500, counter: 13 },
        TestValue::empty(),
        TestValue::empty()
    ];

    // asc

    #[test]
    fn get_split_order_hints_asc_zero_split_counter_full() {
        let split_counter = 0;
        let hints = get_split_order_hints_asc(full_array, split_counter);
        let expected_hints = SplitOrderHints {
            sorted_counters_lt: [0, 0, 0, 0, 0],
            sorted_counters_gte: [5, 7, 11, 13, 17],
            sorted_indexes: [2, 4, 1, 0, 3]
        };
        assert_eq(hints, expected_hints);
    }

    #[test]
    fn get_split_order_hints_asc_non_zero_split_counter_full() {
        let split_counter = 9;
        let hints = get_split_order_hints_asc(full_array, split_counter);
        let expected_hints = SplitOrderHints {
            sorted_counters_lt: [5, 7, 0, 0, 0],
            sorted_counters_gte: [11, 13, 17, 0, 0],
            sorted_indexes: [0, 2, 1, 0, 1]
        };
        assert_eq(hints, expected_hints);
    }

    #[test]
    fn get_split_order_hints_asc_non_zero_split_counter_equal_full() {
        let split_counter = 11; // Equal one of the item's counter.
        let hints = get_split_order_hints_asc(full_array, split_counter);
        let expected_hints = SplitOrderHints {
            sorted_counters_lt: [5, 7, 0, 0, 0],
            sorted_counters_gte: [11, 13, 17, 0, 0],
            sorted_indexes: [0, 2, 1, 0, 1]
        };
        assert_eq(hints, expected_hints);
    }

    #[test]
    fn get_split_order_hints_asc_zero_split_counter_padded_empty() {
        let split_counter = 0;
        let hints = get_split_order_hints_asc(padded_array, split_counter);
        let expected_hints = SplitOrderHints {
            sorted_counters_lt: [0, 0, 0, 0, 0, 0, 0],
            sorted_counters_gte: [5, 7, 11, 13, 17, 0, 0],
            sorted_indexes: [2, 4, 1, 0, 3, 0, 0]
        };
        assert_eq(hints, expected_hints);
    }

    #[test]
    fn get_split_order_hints_asc_non_zero_split_counter_padded_empty() {
        let split_counter = 9;
        let hints = get_split_order_hints_asc(padded_array, split_counter);
        let expected_hints = SplitOrderHints {
            sorted_counters_lt: [5, 7, 0, 0, 0, 0, 0],
            sorted_counters_gte: [11, 13, 17, 0, 0, 0, 0],
            sorted_indexes: [0, 2, 1, 0, 1, 0, 0]
        };
        assert_eq(hints, expected_hints);
    }

    #[test]
    fn get_split_order_hints_asc_non_zero_split_counter_equal_padded_empty() {
        let split_counter = 11;
        let hints = get_split_order_hints_asc(padded_array, split_counter);
        let expected_hints = SplitOrderHints {
            sorted_counters_lt: [5, 7, 0, 0, 0, 0, 0],
            sorted_counters_gte: [11, 13, 17, 0, 0, 0, 0],
            sorted_indexes: [0, 2, 1, 0, 1, 0, 0]
        };
        assert_eq(hints, expected_hints);
    }

    // desc

    #[test]
    fn get_split_order_hints_desc_zero_split_counter_empty() {
        let split_counter = 0;
        let hints = get_split_order_hints_desc(full_array, split_counter);
        let expected_hints = SplitOrderHints {
            sorted_counters_lt: [0, 0, 0, 0, 0],
            sorted_counters_gte: [17, 13, 11, 7, 5],
            sorted_indexes: [2, 0, 3, 4, 1]
        };
        assert_eq(hints, expected_hints);
    }

    #[test]
    fn get_split_order_hints_desc_non_zero_split_counter_empty() {
        let split_counter = 9;
        let hints = get_split_order_hints_desc(full_array, split_counter);
        let expected_hints = SplitOrderHints {
            sorted_counters_lt: [7, 5, 0, 0, 0],
            sorted_counters_gte: [17, 13, 11, 0, 0],
            sorted_indexes: [2, 0, 0, 1, 1]
        };
        assert_eq(hints, expected_hints);
    }

    #[test]
    fn get_split_order_hints_desc_non_zero_split_counter_equal_empty() {
        let split_counter = 11;
        let hints = get_split_order_hints_desc(full_array, split_counter);
        let expected_hints = SplitOrderHints {
            sorted_counters_lt: [7, 5, 0, 0, 0],
            sorted_counters_gte: [17, 13, 11, 0, 0],
            sorted_indexes: [2, 0, 0, 1, 1]
        };
        assert_eq(hints, expected_hints);
    }

    #[test]
    fn get_split_order_hints_desc_zero_split_counter_padded_empty() {
        let split_counter = 0;
        let hints = get_split_order_hints_desc(padded_array, split_counter);
        let expected_hints = SplitOrderHints {
            sorted_counters_lt: [0, 0, 0, 0, 0, 0, 0],
            sorted_counters_gte: [17, 13, 11, 7, 5, 0, 0],
            sorted_indexes: [2, 0, 3, 4, 1, 0, 0]
        };
        assert_eq(hints, expected_hints);
    }

    #[test]
    fn get_split_order_hints_desc_non_zero_split_counter_padded_empty() {
        let split_counter = 9;
        let hints = get_split_order_hints_desc(padded_array, split_counter);
        let expected_hints = SplitOrderHints {
            sorted_counters_lt: [7, 5, 0, 0, 0, 0, 0],
            sorted_counters_gte: [17, 13, 11, 0, 0, 0, 0],
            sorted_indexes: [2, 0, 0, 1, 1, 0, 0]
        };
        assert_eq(hints, expected_hints);
    }

    #[test]
    fn get_split_order_hints_desc_non_zero_split_counter_equal_padded_empty() {
        let split_counter = 11;
        let hints = get_split_order_hints_desc(padded_array, split_counter);
        let expected_hints = SplitOrderHints {
            sorted_counters_lt: [7, 5, 0, 0, 0, 0, 0],
            sorted_counters_gte: [17, 13, 11, 0, 0, 0, 0],
            sorted_indexes: [2, 0, 0, 1, 1, 0, 0]
        };
        assert_eq(hints, expected_hints);
    }
}

