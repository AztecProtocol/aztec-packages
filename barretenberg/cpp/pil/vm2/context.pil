include "context_stack.pil";

// TODO: Usage of sel_error in this whole file has to be reconsidered.

// This is a virtual gadget, which is part of the execution trace.
// This subtrace is focused on managing the changes to the context.
// By default (i.e. when not executing a context-changing opcode),
// the context object is propagated downwards
namespace execution;

    // This the same sel as in execution
    #[skippable_if]
    sel = 0;

    // TODO: make sure that all uses of `sel_error` here really should be "error" only
    // since `sel_error` will not be ` for the REVERT opcode. If we need "error or REVERT",
    // use `sel_failure`.

    // Guaranteed to be boolean because sel_execute_call & sel_execute_static_call are mutually exclusive
    pol commit sel_enter_call;
    // The following selectors will be 0 if there is an error during execution (before the opcode execution step).
    sel_enter_call = sel_execute_call + sel_execute_static_call;
    // CALL & precomputed.first_row are mutually exclusive
    sel_enter_call * precomputed.first_row = 0;

    // sel_exit_call is used to flag if we are returning or reverting or there has been ANY error during execution
    // sel_execute_revert & sel_execute_return are mutually exclusive
    pol commit sel_exit_call;
    sel_exit_call = 1 - (1 - sel_execute_revert - sel_execute_return) * (1 - sel_error);

    // Context columns
    pol commit context_id;
    pol commit parent_id;
    pol commit pc;
    pol commit next_pc;
    pol commit msg_sender;
    pol commit contract_address;
    pol commit bytecode_id;
    pol commit transaction_fee;
    // Constrained boolean by tx trace (for enqueued call) and #[NEXT_IS_STATIC] for nested
    pol commit is_static;

    pol commit parent_calldata_addr;
    pol commit parent_calldata_size;

    pol commit last_child_id;
    pol commit last_child_returndata_addr;
    pol commit last_child_returndata_size;
    pol commit last_child_success; // Careful with this for now...

    // ==== Gas ====
    pol commit l2_gas_limit;
    pol commit da_gas_limit;

    pol commit prev_l2_gas_used;
    pol commit prev_da_gas_used;

    pol commit l2_gas_used;
    pol commit da_gas_used;

    pol commit parent_l2_gas_limit;
    pol commit parent_da_gas_limit;
    pol commit parent_l2_gas_used;
    pol commit parent_da_gas_used;

    // ==== Tree State ===
    pol commit prev_note_hash_tree_root;
    pol commit prev_note_hash_tree_size;
    pol commit prev_num_note_hashes_emitted;

    pol commit prev_nullifier_tree_root;
    pol commit prev_nullifier_tree_size;
    pol commit prev_num_nullifiers_emitted;

    pol commit prev_public_data_tree_root;
    pol commit prev_public_data_tree_size;

    pol commit prev_written_public_data_slots_tree_root;
    pol commit prev_written_public_data_slots_tree_size;

    // L1 to L2 tree doesn't evolve during execution of the AVM
    pol commit l1_l2_tree_root;

    // Next Tree State
    pol commit note_hash_tree_root; // TODO: Constrain root, sizes and emitted not changing unless specific opcode selectors are on
    pol commit note_hash_tree_size;
    pol commit num_note_hashes_emitted;

    pol commit nullifier_tree_root;
    pol commit nullifier_tree_size;
    pol commit num_nullifiers_emitted;

    pol commit public_data_tree_root;
    pol commit public_data_tree_size;

    pol commit written_public_data_slots_tree_root;
    pol commit written_public_data_slots_tree_size;

    // Prev side effects state
    pol commit prev_num_unencrypted_logs;
    pol commit prev_num_l2_to_l1_messages;

    // Next side effects state
    pol commit num_unencrypted_logs;
    pol commit num_l2_to_l1_messages;

    // ==== Helper columns ====
    // TODO: These context modifiers also need to factor in when a new enqueued call occurs
    pol NOT_LAST_EXEC = sel * sel';
    pol SWITCH_CTX = sel_enter_call + sel_exit_call; // Mutually Exclusive
    // We default propagate the context rows if we have more execution steps and we are not exiting or entering a call
    pol DEFAULT_CTX_ROW = (1 - SWITCH_CTX);

    pol PC_JUMP = sel_execute_internal_call + sel_execute_internal_return + sel_execute_jump + sel_execute_jumpi;

    pol commit has_parent_ctx; // == 1 if parent_id != 0;
    has_parent_ctx * (1 - has_parent_ctx) = 0;
    pol commit is_parent_id_inv; // For zero-check of has_parent_ctx
    parent_id * ((1 - has_parent_ctx) * (1 - is_parent_id_inv) + is_parent_id_inv) - has_parent_ctx = 0;

    // We need a flag to indicate when we are exiting a nested context
    pol commit nested_exit_call;
    // If we are exiting and we have a parent ctx
    nested_exit_call = has_parent_ctx * sel_exit_call;

    // next_context_id increments with each invocation of an external call or new enqueued call
    pol commit next_context_id; // Can be replaced by clk

    // We always start enqueued calls with fresh context ids, so the next context id must start at context_id + 1
    #[ENQUEUED_CALL_START_NEXT_CTX_ID]
    enqueued_call_start * (context_id + 1 - next_context_id) = 0;

    // The initial next_context_id = 2, in row = 1
    #[INCR_NEXT_CONTEXT_ID]
    NOT_LAST_EXEC * (next_context_id' - (next_context_id + sel_enter_call + enqueued_call_start')) = 0;

    // nested_exit_call = 1 ==> context_id' = parent_id
    // sel_enter_call   = 1 ==> context_id' = next_context_id
    // otherwise        = 0 ==> context_id' = context_id
    #[CONTEXT_ID_NEXT_ROW]
    NOT_LAST_EXEC * DEFAULT_CTX_ROW * (context_id' - context_id) = 0;
    #[CONTEXT_ID_EXT_CALL]
    NOT_LAST_EXEC * sel_enter_call  * (context_id' - next_context_id) = 0;
    #[CONTEXT_ID_NESTED_EXIT]
    NOT_LAST_EXEC * nested_exit_call   * (context_id' - parent_id) = 0;

    // nested_exit_call = 1 ==> constraints come from lookup
    // sel_enter_call   = 1 ==> parent_id' = context_id
    // otherwise        = 0 ==> parent_id' = parent_id
    #[PARENT_ID_NEXT_ROW]
    NOT_LAST_EXEC * DEFAULT_CTX_ROW * (parent_id' - parent_id) = 0;
    NOT_LAST_EXEC * sel_enter_call  * (parent_id' - context_id) = 0;

    // nested_exit_call = 1 ==> constraints come from lookup
    // sel_enter_call   = 1 ==> pc' = 0
    // internal_call    = 1 ==> pc' = resolved operand 0
    // internal_return  = 1 ==> pc' = internal_call_return_pc (from internal_call.pil)
    // otherwise        = 0 ==> pc' = next_pc
    #[PC_NEXT_ROW_DEFAULT]
    NOT_LAST_EXEC * DEFAULT_CTX_ROW * (1 - PC_JUMP) * (pc' - next_pc) = 0;
    #[PC_NEXT_ROW_EXT_CALL]
    NOT_LAST_EXEC * sel_enter_call  * pc' = 0;

    // nested_exit_call = 1 ==> constraints come from lookup
    // sel_enter_call   = 1 ==> msg_sender' = contract_address
    // otherwise        = 0 ==> msg_sender' = msg_sender
    #[MSG_SENDER_NEXT_ROW]
    NOT_LAST_EXEC * DEFAULT_CTX_ROW * (msg_sender' - msg_sender) = 0;
    NOT_LAST_EXEC * sel_enter_call  * (msg_sender' - contract_address) = 0;

    // nested_exit_call = 1 ==> constraints come from lookup
    // sel_enter_call   = 1 ==> contract_address' = register[2] (intermediate register 3 from execution trace)
    // otherwise        = 0 ==> contract_address' = contract_address
    #[CONTRACT_ADDR_NEXT_ROW]
    NOT_LAST_EXEC * DEFAULT_CTX_ROW * (contract_address' - contract_address) = 0;
    NOT_LAST_EXEC * sel_enter_call  * (contract_address' - register[2]) = 0;

    // bytecode_id is set from the context stack when entering a call and propagated
    // from row to row as we do for others, except on context change.
    #[BYTECODE_ID_NEXT_ROW]
    NOT_LAST_EXEC * DEFAULT_CTX_ROW * (bytecode_id' - bytecode_id) = 0;

    // transaction fee gets set at the start of an enqueued call and remains the same throughout
    // TODO(dbanks12): tracegen and test this
    #[TRANSACTION_FEE_NEXT_ROW]
    (1 - enqueued_call_end) * (transaction_fee' - transaction_fee) = 0;
    // NOTE: don't need to say NOT_LAST_EXEC because last execution row must be an enqueued call end
    // NOTE: on enqueued_call_start, transaction_fee is set via the DISPATCH lookup from tx trace to execution.

    // nested_exit_call = 1 ==> constraints come from lookup
    // sel_enter_call   = 1 && static_call = 1 ==> is_static' = 1
    // sel_enter_call   = 1 && static_call = 0 ==> is_static' = 0
    // otherwise        = 0 ==> is_static' = is_static
    #[IS_STATIC_NEXT_ROW]
    NOT_LAST_EXEC * DEFAULT_CTX_ROW * (is_static' - is_static) = 0;
    NOT_LAST_EXEC * sel_enter_call  * (is_static' - sel_execute_static_call) = 0;

    // nested_exit_call = 1 ==> constraints come from lookup
    // sel_enter_call   = 1 ==> parent_calldata_addr' = rop[4] (resolved operand 5 from execution trace)
    // otherwise        = 0 ==> parent_calldata_addr' = parent_calldata_addr
    #[CD_OFFSET_NEXT_ROW]
    NOT_LAST_EXEC * DEFAULT_CTX_ROW * (parent_calldata_addr' - parent_calldata_addr) = 0;
    NOT_LAST_EXEC * sel_enter_call  * (parent_calldata_addr' - rop[4]) = 0;

    // nested_exit_call = 1 ==> constraints come from lookup
    // sel_enter_call   = 1 ==> parent_calldata_size' = register[3] (register 4 from execution trace)
    // otherwise        = 0 ==> parent_calldata_size' = parent_calldata_size
    #[CD_SIZE_NEXT_ROW]
    NOT_LAST_EXEC * DEFAULT_CTX_ROW * (parent_calldata_size' - parent_calldata_size) = 0;
    NOT_LAST_EXEC * sel_enter_call  * (parent_calldata_size' - register[3]) = 0;

    pol NESTED_RET_REV_ONLY = nested_exit_call * (1 - sel_error);

    // NESTED_RET_REV_ONLY = 1 ==> rop[1] (resolved operand 2 from execution trace)
    // sel_error           = 1 ==> last_child_returndata_addr' = 0;
    // sel_enter_call      = 1 ==> last_child_returndata_addr' = 0;
    // enqueued_call_start = 1 ==> last_child_returndata_addr  = 0; <-- This row is 0
    // otherwise           = 0 ==> last_child_returndata_addr' = last_child_returndata_addr
    #[RET_REV_RD_ADDR]
    NOT_LAST_EXEC * NESTED_RET_REV_ONLY * (last_child_returndata_addr' - rop[1]) = 0;
    #[NEXT_RD_ADDR_IS_ZERO]
    NOT_LAST_EXEC * (sel_enter_call + sel_error) * last_child_returndata_addr' = 0;
    #[RD_ADDR_IS_ZERO]
    enqueued_call_start * last_child_returndata_addr = 0;
    #[PROPAGATE_RD_ADDR]
    NOT_LAST_EXEC * DEFAULT_CTX_ROW * (last_child_returndata_addr' - last_child_returndata_addr) = 0;

    // NESTED_RET_REV_ONLY = 1 ==> register[0] (intermediate register 1 from execution trace)
    // sel_error           = 1 ==> last_child_returndata_size' = 0;
    // sel_enter_call      = 1 ==> last_child_returndata_size' = 0;
    // enqueued_call_start = 1 ==> last_child_returndata_size  = 0; <-- Current row is 0
    // otherwise           = 0 ==> last_child_returndata_size' = last_child_returndata_size
    #[RET_REV_RD_SIZE]
    NOT_LAST_EXEC * NESTED_RET_REV_ONLY  * (last_child_returndata_size' - register[0]) = 0;
    #[NEXT_RD_SIZE_IS_ZERO]
    NOT_LAST_EXEC * (sel_enter_call + sel_error) * last_child_returndata_size' = 0;
    #[RD_SIZE_IS_ZERO]
    enqueued_call_start * last_child_returndata_addr = 0;
    #[PROPAGATE_RD_SIZE]
    NOT_LAST_EXEC * DEFAULT_CTX_ROW * (last_child_returndata_size' - last_child_returndata_size) = 0;

    // nested_exit_call    = 1 ==> last_child_id' = context_id;  <-- sel_exit_call includes error case
    // sel_enter_call      = 1 ==> last_child_id' = 0;
    // enqueued_call_start = 1 ==> last_child_id = 0; <-- Current row is 0
    // otherwise           = 0 ==> last_child_id' = last_child_id;
    #[EXIT_CALL_LAST_CHILD_ID]
    NOT_LAST_EXEC * nested_exit_call * (last_child_id' - context_id) = 0;
    #[ENTER_CALL_LAST_CHILD_ID]
    NOT_LAST_EXEC * sel_enter_call * last_child_id' = 0;
    #[LAST_CHILD_ID_IS_ZERO]
    enqueued_call_start * last_child_id = 0;
    #[PROPAGATE_LAST_CHILD_ID]
    NOT_LAST_EXEC * DEFAULT_CTX_ROW * (last_child_id' - last_child_id) = 0;

    // nested_exit_call = 1 ==> l2_gas_limit' = parent_l2_gas_limit
    // sel_enter_call   = 1 ==> constraints come from call logic
    // otherwise        = 0 ==> l2_gas_limit' = l2_gas_limit
    #[L2_GAS_LIMIT_NEXT_ROW]
    NOT_LAST_EXEC * DEFAULT_CTX_ROW * (l2_gas_limit' - l2_gas_limit) = 0;
    #[L2_GAS_LIMIT_RESTORE_ON_EXIT]
    NOT_LAST_EXEC * nested_exit_call * (l2_gas_limit' - parent_l2_gas_limit) = 0;

    // nested_exit_call = 1 ==> da_gas_limit' = parent_da_gas_limit
    // sel_enter_call   = 1 ==> constraints come from call logic
    // otherwise        = 0 ==> da_gas_limit' = da_gas_limit
    #[DA_GAS_LIMIT_NEXT_ROW]
    NOT_LAST_EXEC * DEFAULT_CTX_ROW * (da_gas_limit' - da_gas_limit) = 0;
    #[DA_GAS_LIMIT_RESTORE_ON_EXIT]
    NOT_LAST_EXEC * nested_exit_call * (da_gas_limit' - parent_da_gas_limit) = 0;

    // nested_exit_call = 1 ==> constraints come from lookup
    // sel_enter_call   = 1 ==> parent_l2_gas_limit' = l2_gas_limit
    // otherwise        = 0 ==> parent_l2_gas_limit' = parent_l2_gas_limit
    #[PARENT_L2_GAS_LIMIT_NEXT_ROW]
    NOT_LAST_EXEC * DEFAULT_CTX_ROW * (parent_l2_gas_limit' - parent_l2_gas_limit) = 0;
    #[PARENT_L2_GAS_LIMIT_STORE_ON_ENTER]
    NOT_LAST_EXEC * sel_enter_call * (parent_l2_gas_limit' - l2_gas_limit) = 0;

    // nested_exit_call = 1 ==> constraints come from lookup
    // sel_enter_call   = 1 ==> parent_da_gas_limit' = da_gas_limit
    // otherwise        = 0 ==> parent_da_gas_limit' = parent_da_gas_limit
    #[PARENT_DA_GAS_LIMIT_NEXT_ROW]
    NOT_LAST_EXEC * DEFAULT_CTX_ROW * (parent_da_gas_limit' - parent_da_gas_limit) = 0;
    #[PARENT_DA_GAS_LIMIT_STORE_ON_ENTER]
    NOT_LAST_EXEC * sel_enter_call * (parent_da_gas_limit' - da_gas_limit) = 0;

    // nested_exit_call = 1 ==> constraints come from lookup
    // sel_enter_call   = 1 ==> parent_l2_gas_used' = l2_gas_used
    // otherwise        = 0 ==> parent_l2_gas_used' = parent_l2_gas_used
    #[PARENT_L2_GAS_USED_NEXT_ROW]
    NOT_LAST_EXEC * DEFAULT_CTX_ROW * (parent_l2_gas_used' - parent_l2_gas_used) = 0;
    #[PARENT_L2_GAS_USED_STORE_ON_ENTER]
    NOT_LAST_EXEC * sel_enter_call * (parent_l2_gas_used' - l2_gas_used) = 0;

    // nested_exit_call = 1 ==> constraints come from lookup
    // sel_enter_call   = 1 ==> parent_da_gas_used' = da_gas_used
    // otherwise        = 0 ==> parent_da_gas_used' = parent_da_gas_used
    #[PARENT_DA_GAS_USED_NEXT_ROW]
    NOT_LAST_EXEC * DEFAULT_CTX_ROW * (parent_da_gas_used' - parent_da_gas_used) = 0;
    #[PARENT_DA_GAS_USED_STORE_ON_ENTER]
    NOT_LAST_EXEC * sel_enter_call * (parent_da_gas_used' - da_gas_used) = 0;

    #[CTX_STACK_CALL]
    sel_enter_call {
        next_context_id,
        context_id,
        parent_id,
        next_pc,
        msg_sender,
        contract_address,
        bytecode_id,
        is_static,
        parent_calldata_addr,
        parent_calldata_size,
        parent_l2_gas_limit,
        parent_da_gas_limit,
        parent_l2_gas_used,
        parent_da_gas_used,
        note_hash_tree_root,
        note_hash_tree_size,
        num_note_hashes_emitted,
        nullifier_tree_root,
        nullifier_tree_size,
        num_nullifiers_emitted,
        public_data_tree_root,
        public_data_tree_size,
        written_public_data_slots_tree_root,
        written_public_data_slots_tree_size,
        num_unencrypted_logs,
        num_l2_to_l1_messages
    } in
    context_stack.sel {
        context_stack.entered_context_id,
        context_stack.context_id,
        context_stack.parent_id,
        context_stack.next_pc,
        context_stack.msg_sender,
        context_stack.contract_address,
        context_stack.bytecode_id,
        context_stack.is_static,
        context_stack.parent_calldata_addr,
        context_stack.parent_calldata_size,
        context_stack.parent_l2_gas_limit,
        context_stack.parent_da_gas_limit,
        context_stack.parent_l2_gas_used,
        context_stack.parent_da_gas_used,
        context_stack.note_hash_tree_root,
        context_stack.note_hash_tree_size,
        context_stack.num_note_hashes_emitted,
        context_stack.nullifier_tree_root,
        context_stack.nullifier_tree_size,
        context_stack.num_nullifiers_emitted,
        context_stack.public_data_tree_root,
        context_stack.public_data_tree_size,
        context_stack.written_public_data_slots_tree_root,
        context_stack.written_public_data_slots_tree_size,
        context_stack.num_unencrypted_logs,
        context_stack.num_l2_to_l1_messages
    };

    // If we are reverting or erroring a nested call, we restore the entire ctx from the stack
    pol commit rollback_context;
    rollback_context * (1 - rollback_context) = 0;
    // Constrained to be boolean based on previous relations
    rollback_context = nested_exit_call * (1 - sel_execute_return);

    #[CTX_STACK_ROLLBACK]
    rollback_context {
        context_id,
        context_id', // constrained to be parent_id
        parent_id',
        pc',
        msg_sender',
        contract_address',
        bytecode_id',
        is_static',
        parent_calldata_addr',
        parent_calldata_size',
        parent_l2_gas_limit',
        parent_da_gas_limit',
        parent_l2_gas_used',
        parent_da_gas_used',
        // Revert trees and side effects
        prev_note_hash_tree_root',
        prev_note_hash_tree_size',
        prev_num_note_hashes_emitted',
        prev_nullifier_tree_root',
        prev_nullifier_tree_size',
        prev_num_nullifiers_emitted',
        prev_public_data_tree_root',
        prev_public_data_tree_size',
        prev_written_public_data_slots_tree_root',
        prev_written_public_data_slots_tree_size',
        prev_num_unencrypted_logs',
        prev_num_l2_to_l1_messages'
    } in
    context_stack.sel {
        context_stack.entered_context_id,
        context_stack.context_id,
        context_stack.parent_id,
        context_stack.next_pc,
        context_stack.msg_sender,
        context_stack.contract_address,
        context_stack.bytecode_id,
        context_stack.is_static,
        context_stack.parent_calldata_addr,
        context_stack.parent_calldata_size,
        context_stack.parent_l2_gas_limit,
        context_stack.parent_da_gas_limit,
        context_stack.parent_l2_gas_used,
        context_stack.parent_da_gas_used,
        context_stack.note_hash_tree_root,
        context_stack.note_hash_tree_size,
        context_stack.num_note_hashes_emitted,
        context_stack.nullifier_tree_root,
        context_stack.nullifier_tree_size,
        context_stack.num_nullifiers_emitted,
        context_stack.public_data_tree_root,
        context_stack.public_data_tree_size,
        context_stack.written_public_data_slots_tree_root,
        context_stack.written_public_data_slots_tree_size,
        context_stack.num_unencrypted_logs,
        context_stack.num_l2_to_l1_messages
    };

    pol commit nested_return;
    nested_return = nested_exit_call * sel_execute_return * (1 - sel_error);
    #[CTX_STACK_RETURN]
    nested_return {
        context_id,
        context_id', // constrained to be parent_id
        parent_id',
        pc',
        msg_sender',
        contract_address',
        bytecode_id',
        is_static',
        parent_calldata_addr',
        parent_calldata_size',
        parent_l2_gas_limit',
        parent_da_gas_limit',
        parent_l2_gas_used',
        parent_da_gas_used'
        // We don't restore trees or side effects in return since we adopt the nested state after end.
    } in
    context_stack.sel {
        context_stack.entered_context_id,
        context_stack.context_id,
        context_stack.parent_id,
        context_stack.next_pc,
        context_stack.msg_sender,
        context_stack.contract_address,
        context_stack.bytecode_id,
        context_stack.is_static,
        context_stack.parent_calldata_addr,
        context_stack.parent_calldata_size,
        context_stack.parent_l2_gas_limit,
        context_stack.parent_da_gas_limit,
        context_stack.parent_l2_gas_used,
        context_stack.parent_da_gas_used
    };

    // ==== GAS USED CONSTRAINING ====

    // If any error happened during execution (i.e., sel_error=1), all gas should be consumed.
    pol SEL_CONSUMED_ALL_GAS = sel_error;
    (l2_gas_limit - PREV_GAS_PLUS_USAGE_L2) * SEL_CONSUMED_ALL_GAS + PREV_GAS_PLUS_USAGE_L2 - l2_gas_used = 0;
    (da_gas_limit - PREV_GAS_PLUS_USAGE_DA) * SEL_CONSUMED_ALL_GAS + PREV_GAS_PLUS_USAGE_DA - da_gas_used = 0;

    // nested_exit_call = 1 <==> prev_gas_used' = parent_gas_used + gas_used
    // sel_enter_call   = 1 <==> prev_gas_used' = 0
    // otherwise        = 0 <==> prev_gas_used' = gas_used
    #[L2_GAS_USED_CONTINUITY]
    NOT_LAST_EXEC * DEFAULT_CTX_ROW * (l2_gas_used - prev_l2_gas_used') = 0;
    #[L2_GAS_USED_ZERO_AFTER_CALL]
    NOT_LAST_EXEC * sel_enter_call * prev_l2_gas_used' = 0;
    #[L2_GAS_USED_INGEST_AFTER_EXIT]
    NOT_LAST_EXEC * nested_exit_call * (parent_l2_gas_used + l2_gas_used - prev_l2_gas_used') = 0;
    #[DA_GAS_USED_CONTINUITY]
    NOT_LAST_EXEC * DEFAULT_CTX_ROW * (da_gas_used - prev_da_gas_used') = 0;
    #[DA_GAS_USED_ZERO_AFTER_CALL]
    NOT_LAST_EXEC * sel_enter_call * prev_da_gas_used' = 0;
    #[DA_GAS_USED_INGEST_AFTER_EXIT]
    NOT_LAST_EXEC * nested_exit_call * (parent_da_gas_used + da_gas_used - prev_da_gas_used') = 0;
    // Tree and sideffect state should not change in default rows and nested returns.
    // In top level exits, the state is constrained by the tx > execution lookup
    // In nested reverts, the state is constrained by the rollback_context lookup
    pol DEFAULT_OR_NESTED_RETURN = DEFAULT_CTX_ROW + nested_return; // Mutually exclusive
    #[NOTE_HASH_TREE_ROOT_CONTINUITY]
    NOT_LAST_EXEC * DEFAULT_OR_NESTED_RETURN * (note_hash_tree_root - prev_note_hash_tree_root') = 0;
    #[NOTE_HASH_TREE_SIZE_CONTINUITY]
    NOT_LAST_EXEC * DEFAULT_OR_NESTED_RETURN * (note_hash_tree_size - prev_note_hash_tree_size') = 0;
    #[NUM_NOTE_HASHES_EMITTED_CONTINUITY]
    NOT_LAST_EXEC * DEFAULT_OR_NESTED_RETURN * (num_note_hashes_emitted - prev_num_note_hashes_emitted') = 0;
    #[NULLIFIER_TREE_ROOT_CONTINUITY]
    NOT_LAST_EXEC * DEFAULT_OR_NESTED_RETURN * (nullifier_tree_root - prev_nullifier_tree_root') = 0;
    #[NULLIFIER_TREE_SIZE_CONTINUITY]
    NOT_LAST_EXEC * DEFAULT_OR_NESTED_RETURN * (nullifier_tree_size - prev_nullifier_tree_size') = 0;
    #[NUM_NULLIFIERS_EMITTED_CONTINUITY]
    NOT_LAST_EXEC * DEFAULT_OR_NESTED_RETURN * (num_nullifiers_emitted - prev_num_nullifiers_emitted') = 0;
    #[PUBLIC_DATA_TREE_ROOT_CONTINUITY]
    NOT_LAST_EXEC * DEFAULT_OR_NESTED_RETURN * (public_data_tree_root - prev_public_data_tree_root') = 0;
    #[PUBLIC_DATA_TREE_SIZE_CONTINUITY]
    NOT_LAST_EXEC * DEFAULT_OR_NESTED_RETURN * (public_data_tree_size - prev_public_data_tree_size') = 0;
    #[WRITTEN_PUBLIC_DATA_SLOTS_TREE_ROOT_CONTINUITY]
    NOT_LAST_EXEC * DEFAULT_OR_NESTED_RETURN * (written_public_data_slots_tree_root - prev_written_public_data_slots_tree_root') = 0;
    #[WRITTEN_PUBLIC_DATA_SLOTS_TREE_SIZE_CONTINUITY]
    NOT_LAST_EXEC * DEFAULT_OR_NESTED_RETURN * (written_public_data_slots_tree_size - prev_written_public_data_slots_tree_size') = 0;
    // L1 to L2 message tree is immutable in a tx
    #[L1_L2_TREE_ROOT_CONTINUITY]
    NOT_LAST_EXEC * (l1_l2_tree_root - l1_l2_tree_root') = 0;

    #[NUM_UNENCRYPTED_LOGS_CONTINUITY]
    NOT_LAST_EXEC * DEFAULT_OR_NESTED_RETURN * (num_unencrypted_logs - prev_num_unencrypted_logs') = 0;
    #[NUM_L2_TO_L1_MESSAGES_CONTINUITY]
    NOT_LAST_EXEC * DEFAULT_OR_NESTED_RETURN * (num_l2_to_l1_messages - prev_num_l2_to_l1_messages') = 0;
