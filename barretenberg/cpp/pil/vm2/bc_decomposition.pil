// This file owns the bytecode columns, and other helper columns.
// TODO: This does NOT support empty bytecode.
// TODO: Fully constrain.
namespace bc_decomposition;

pol commit sel;
// No relations will be checked if this identity is satisfied.
#[skippable_if]
sel = 0;

// Size of the sliding window.
// This includes the "current byte" and the WINDOW_SIZE - 1 lookahead bytes.
pol WINDOW_SIZE = 36;

pol commit id;
// pc counts from 0 to bytecode_length - 1.
pol commit pc;
// bytes_remaining counts from bytecode_length to 1.
pol commit bytes_remaining;

// TODO: Constrain pc is increasing until contract switch.
// TODO: Constrain bytes_remaining is decreasing until contract switch.
// TODO: Constrain bytes_remaining = 1 iff contract switch.
// This column should be 1 iff the contract is switching.
pol commit last_of_contract;
last_of_contract * (1 - last_of_contract) = 0;

// This is the most important column. It contains the bytecode one byte at a time on each row.
// If the TX uses several bytecodes, they should go one after the other in this column.
pol commit bytes;
// This constrains that the bytes are in the range 0 to 255.
#[LOOKUP_BYTECODE_BYTES_ARE_BYTES]
sel { bytes } in precomputed.sel_range_8 { precomputed.clk };

// These are helper columns that will be used for bytecode decomposition.
pol commit bytes_pc_plus_1, bytes_pc_plus_2, bytes_pc_plus_3, bytes_pc_plus_4, bytes_pc_plus_5,
           bytes_pc_plus_6, bytes_pc_plus_7, bytes_pc_plus_8, bytes_pc_plus_9, bytes_pc_plus_10,
           bytes_pc_plus_11, bytes_pc_plus_12, bytes_pc_plus_13, bytes_pc_plus_14, bytes_pc_plus_15,
           bytes_pc_plus_16, bytes_pc_plus_17, bytes_pc_plus_18, bytes_pc_plus_19, bytes_pc_plus_20,
           bytes_pc_plus_21, bytes_pc_plus_22, bytes_pc_plus_23, bytes_pc_plus_24, bytes_pc_plus_25,
           bytes_pc_plus_26, bytes_pc_plus_27, bytes_pc_plus_28, bytes_pc_plus_29, bytes_pc_plus_30,
           bytes_pc_plus_31, bytes_pc_plus_32, bytes_pc_plus_33, bytes_pc_plus_34, bytes_pc_plus_35;

// DECOMPOSITION DOES NOT GO OVER THE END OF THE BYTECODE
//
// We need to constrain that the bytes_pc_plus_n do not go over the end of the bytecode.
// In that case we expect there to be 0s. We need a few extra helper columns and some tricks.
//
// The basic idea is that, from "bytes_remaining" we will derive some selectors for each
// bytes_pc_plus_n. A "mask" of sorts.
//
// (1) First we need to know how many bytes we are going to read into the sliding window.
// This is the minimum of WINDOW_SIZE and bytes_remaining.
//
// (2) The first step will be to compute a UNARY (not binary) representation of the bytes_to_read.
// For example, if bytes_to_read = 3, then the unary representation is 111. This will mean
// That only the first three bytes are valid. NB: Using this unary trick saves us from doing
// WINDOW_SIZE+ lookups and we only do 1 lookup.
//
// (3) Then we will expand that uniary representation into a set of WINDOW_SIZE selectors. This is done
// via the usual reconstruction technique.
//
// (4) Then we apply the selectors to the bytes_pc_plus_n columns.
//
// Example:
// Suppose a bytecode = [0x0, 0x1, ..., 0xA, 0xB, 0xC, 0xD] of size 40.
//
//   pc | bytes_remaining | bytes_to_read | bytes |  +1 |  +2 | ...
// -----+-----------------+---------------+-------+-----+-----+-----
//    0 |              40 |   WINDOW_SIZE |   0x1 | 0x2 | 0x3 | ...
//    1 |              39 |   WINDOW_SIZE |   0x2 | 0x3 | 0x4 | ...
//    2 |              38 |   WINDOW_SIZE |   0x3 | 0x4 | 0x5 | ...
//    ...
//   36 |               4 |             4 |   0xA | 0xB | 0xC | ...
//   37 |               3 |             3 |   0xB | 0xC | 0xD | ...
//   38 |               2 |             2 |   0xC | 0xD | *** | ...
//   39 |               1 |             1 |   0xD | *** | *** | ...

pol commit bytes_to_read;
pol commit sel_overflow_correction_needed;
sel_overflow_correction_needed * (1 - sel_overflow_correction_needed) = 0;
// We need to constrain bytes_to_read = min(WINDOW_SIZE, bytes_remaining). First we do...
#[BYTECODE_OVERFLOW_CORRECTION_VALUE]
sel * ((1 - sel_overflow_correction_needed) * (bytes_to_read - WINDOW_SIZE) + sel_overflow_correction_needed * (bytes_to_read - bytes_remaining)) = 0;
// And then we do sel_overflow_correction_needed = 1 iff bytes_remaining < WINDOW_SIZE.
// #[LOOKUP_BYTECODE_REMAINING_U16]
// sel_overflow_correction_needed { bytes_remaining - WINDOW_SIZE } in precomputed.sel_u16 { precomputed.clk }

pol commit bytes_to_read_unary;
#[LOOKUP_BYTECODE_TO_READ_UNARY]
sel { bytes_to_read, bytes_to_read_unary } in precomputed.sel_unary { precomputed.clk, precomputed.as_unary };

// We don't need sel_pc_plus_0 because we always read the current byte.
pol commit sel_pc_plus_0, sel_pc_plus_1, sel_pc_plus_2, sel_pc_plus_3, sel_pc_plus_4,
           sel_pc_plus_5, sel_pc_plus_6, sel_pc_plus_7, sel_pc_plus_8, sel_pc_plus_9,
           sel_pc_plus_10, sel_pc_plus_11, sel_pc_plus_12, sel_pc_plus_13, sel_pc_plus_14,
           sel_pc_plus_15, sel_pc_plus_16, sel_pc_plus_17, sel_pc_plus_18, sel_pc_plus_19,
           sel_pc_plus_20, sel_pc_plus_21, sel_pc_plus_22, sel_pc_plus_23, sel_pc_plus_24,
           sel_pc_plus_25, sel_pc_plus_26, sel_pc_plus_27, sel_pc_plus_28, sel_pc_plus_29,
           sel_pc_plus_30, sel_pc_plus_31, sel_pc_plus_32, sel_pc_plus_33, sel_pc_plus_34,
           sel_pc_plus_35;

#[BYTECODE_UNARY_RECONSTRUCTION]
bytes_to_read_unary = sel * (/*sel_pc_plus_0*/ 2**0 + sel_pc_plus_1  * 2**1  + sel_pc_plus_2  * 2**2  + sel_pc_plus_3  * 2**3  + sel_pc_plus_4  * 2**4  +
                             sel_pc_plus_5  * 2**5  + sel_pc_plus_6  * 2**6  + sel_pc_plus_7  * 2**7  + sel_pc_plus_8  * 2**8  + sel_pc_plus_9  * 2**9  +
                             sel_pc_plus_10 * 2**10 + sel_pc_plus_11 * 2**11 + sel_pc_plus_12 * 2**12 + sel_pc_plus_13 * 2**13 + sel_pc_plus_14 * 2**14 +
                             sel_pc_plus_15 * 2**15 + sel_pc_plus_16 * 2**16 + sel_pc_plus_17 * 2**17 + sel_pc_plus_18 * 2**18 + sel_pc_plus_19 * 2**19 +
                             sel_pc_plus_20 * 2**20 + sel_pc_plus_21 * 2**21 + sel_pc_plus_22 * 2**22 + sel_pc_plus_23 * 2**23 + sel_pc_plus_24 * 2**24 +
                             sel_pc_plus_25 * 2**25 + sel_pc_plus_26 * 2**26 + sel_pc_plus_27 * 2**27 + sel_pc_plus_28 * 2**28 + sel_pc_plus_29 * 2**29 +
                             sel_pc_plus_30 * 2**30 + sel_pc_plus_31 * 2**31 + sel_pc_plus_32 * 2**32 + sel_pc_plus_33 * 2**33 + sel_pc_plus_34 * 2**34 +
                             sel_pc_plus_35 * 2**35);
// Note: the above relation constrains the selectors to be binary when taken together with the lookup into unary.

// Constrain shifted columns.
bytes_pc_plus_1 = sel_pc_plus_1 * bytes';
bytes_pc_plus_2 = sel_pc_plus_2 * bytes_pc_plus_1';
bytes_pc_plus_3 = sel_pc_plus_3 * bytes_pc_plus_2';
bytes_pc_plus_4 = sel_pc_plus_4 * bytes_pc_plus_3';
bytes_pc_plus_5 = sel_pc_plus_5 * bytes_pc_plus_4';
bytes_pc_plus_6 = sel_pc_plus_6 * bytes_pc_plus_5';
bytes_pc_plus_7 = sel_pc_plus_7 * bytes_pc_plus_6';
bytes_pc_plus_8 = sel_pc_plus_8 * bytes_pc_plus_7';
bytes_pc_plus_9 = sel_pc_plus_9 * bytes_pc_plus_8';
bytes_pc_plus_10 = sel_pc_plus_10 * bytes_pc_plus_9';
bytes_pc_plus_11 = sel_pc_plus_11 * bytes_pc_plus_10';
bytes_pc_plus_12 = sel_pc_plus_12 * bytes_pc_plus_11';
bytes_pc_plus_13 = sel_pc_plus_13 * bytes_pc_plus_12';
bytes_pc_plus_14 = sel_pc_plus_14 * bytes_pc_plus_13';
bytes_pc_plus_15 = sel_pc_plus_15 * bytes_pc_plus_14';
bytes_pc_plus_16 = sel_pc_plus_16 * bytes_pc_plus_15';
bytes_pc_plus_17 = sel_pc_plus_17 * bytes_pc_plus_16';
bytes_pc_plus_18 = sel_pc_plus_18 * bytes_pc_plus_17';
bytes_pc_plus_19 = sel_pc_plus_19 * bytes_pc_plus_18';
bytes_pc_plus_20 = sel_pc_plus_20 * bytes_pc_plus_19';
bytes_pc_plus_21 = sel_pc_plus_21 * bytes_pc_plus_20';
bytes_pc_plus_22 = sel_pc_plus_22 * bytes_pc_plus_21';
bytes_pc_plus_23 = sel_pc_plus_23 * bytes_pc_plus_22';
bytes_pc_plus_24 = sel_pc_plus_24 * bytes_pc_plus_23';
bytes_pc_plus_25 = sel_pc_plus_25 * bytes_pc_plus_24';
bytes_pc_plus_26 = sel_pc_plus_26 * bytes_pc_plus_25';
bytes_pc_plus_27 = sel_pc_plus_27 * bytes_pc_plus_26';
bytes_pc_plus_28 = sel_pc_plus_28 * bytes_pc_plus_27';
bytes_pc_plus_29 = sel_pc_plus_29 * bytes_pc_plus_28';
bytes_pc_plus_30 = sel_pc_plus_30 * bytes_pc_plus_29';
bytes_pc_plus_31 = sel_pc_plus_31 * bytes_pc_plus_30';
bytes_pc_plus_32 = sel_pc_plus_32 * bytes_pc_plus_31';
bytes_pc_plus_33 = sel_pc_plus_33 * bytes_pc_plus_32';
bytes_pc_plus_34 = sel_pc_plus_34 * bytes_pc_plus_33';
bytes_pc_plus_35 = sel_pc_plus_35 * bytes_pc_plus_34';